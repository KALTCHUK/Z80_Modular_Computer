10 REM --------------------------------
20 REM MANDEL V3.1 - PLOT MANDELBROT SET
30 REM BY KALTCHUK, SEP/2021
40 REM --------------------------------
50 FCOR$=" 37,34,33,32,31,36,35,34,33,32,31,36,35,34,33,32,31,36"
60 BCOR$=" 40,41,42,43,44,45,46,41,42,43,44,45,46,41,42,43,44,45"
70 P$=" #*%%&&;;::::......"
80 E$=CHR$(27)
90 CLRSCR$=E$+"[2J"
100 CURHOM$=E$+"[H"
110 PRINT CLRSCR$;CURHOM$
120 FOR I=0 TO 70
130 FOR J=44 TO 0 STEP -1
140 X=(I-52)/31:Y=(J-22)/31:XA=0:YA=0:ITER=0
150 XTEMP=XA*XA-YA*YA+X:YA=2*XA*YA+Y:XA=XTEMP:ITER=ITER+1:C=XA*XA+YA*YA
160 IF C<=4 AND ITER<200 THEN GOTO 150
170 K=1+CINT(C)
180 IF K>17 THEN K=17
190 T$=STR$(I+1)
200 P=1:IF I>=9 THEN P=2
210 X$=MID$(T$,2,P)
220 T$=STR$(J+1)
230 P=1:IF J>=9 THEN P=2
240 Y$=MID$(T$,2,P)
250 CP=3*K-1
260 PRINT E$;"[";Y$;";";X$;"H";E$;"[";MID$(BCOR$,CP,2);"m";E$;"[";MID$(FCOR$,CP,2);"m";MID$(P$,K,1)
270 NEXT J
280 NEXT I
290 PRINT E$;"[47;1H";E$;"[40m";E$;"[32m";
300 PRINT "MANDEL v3.0 - by Kaltchuk, 09/2021."
310 END
