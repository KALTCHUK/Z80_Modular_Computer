1 PRINT CHR$(26)
2 PRINT TAB(30) "CHIEF":PRINT
4 PRINT TAB(15) "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
6 PRINT:PRINT:PRINT
10 PRINT "I AM CHIEF NUMBERS FREEK, THE GREAT INDIAN MATH GOD."
20 PRINT "ARE YOU READY TO TAKE THE TEST YOU CALLED ME OUT FOR";
30 INPUT A$
40 IF LEFT$(A$,1)="Y" OR LEFT$(A$,1)="y" THEN 60
50 PRINT "SHUT UP, PALE FACE WITH WISE TONGUE."
60 PRINT:PRINT "TAKE A NUMBER AND ADD 3. DIVIDE THIS NUMBER BY 5 AND"
70 PRINT "MULTIPLY BY 8. DIVIDE BY 5 AND ADD THE SAME. SUBTRACT 1.":PRINT
80 PRINT "WHAT DO YOU HAVE";
90 INPUT B
95 PRINT
100 LET C = (B+1-5)*5/8*5-3
110 PRINT "I BET YOUR NUMBER WAS";C;CHR$(8);". AM I RIGHT";
120 INPUT D$
130 IF LEFT$(D$,1)="Y" OR LEFT$(D$,1)="y" THEN 500
140 PRINT:PRINT "WHAT WAS YOUR ORIGINAL NUMBER";
150 INPUT K
155 LET F=K+3
160 LET G=F/5
170 LET H=G*8
180 LET I=H/5+5
190 LET J=I-1
200 PRINT:PRINT "SO YOU THINK YOU'RE SO SMART, EH?"
210 PRINT "NOW WATCH.":PRINT
230 PRINT K"PLUS 3 EQUALS"F;CHR$(8);". THIS DIVIDED BY 5 EQUALS"G;CHR$(8);";"
240 PRINT " THIS TIMES 8 EQUALS"H;CHR$(8);". IF WE DIVIDE BY 5 AND ADD 5,"
250 PRINT " WE GET"I;CHR$(8);", WHICH, MINUS 1, EQUALS"J;CHR$(8);"."
260 PRINT:PRINT "NOW DO YOU BELIEVE ME";
270 INPUT Z$
290 IF LEFT$(Z$,1)="Y" OR LEFT$(Z$,1)="y" THEN 500
295 PRINT:PRINT "YOU HAVE MADE ME MAD!!!"
300 PRINT "THERE MUST BE A GREAT LIGHTNING BOLT!"
310 PRINT:PRINT
320 FOR XX=1 TO 2000:NEXT
330 FOR X=30 TO 22 STEP -1
340 PRINT TAB(X) "X X"
350 NEXT X
360 PRINT TAB(21) "X XXX"
370 PRINT TAB(20) "X   X"
380 PRINT TAB(19) "XX X"
390 FOR Y=20 TO 13 STEP -1
400 PRINT TAB(Y) "X X"
410 NEXT Y
420 PRINT TAB(12) "XX"
430 PRINT TAB(11) "X"
440 PRINT TAB(10) "*"
450 PRINT:PRINT"#########################":PRINT
470 PRINT "I HOPE YOU BELIEVE ME NOW, FOR YOUR SAKE!!"
500 PRINT:INPUT "AGAIN";R$:
    IF LEFT$(R$,1)="Y" OR LEFT$(R$,1)="y" THEN 60 ELSE 510
510 PRINT:PRINT "BYE!!!":PRINT:PRINT:PRINT
520 RUN "MENU"
